# This file is generated by running "yarn install" inside your project.
# Manual changes might be lost - proceed with caution!

{ lib, bash, coreutils, nodejs, perl, stdenv, writeText }:

with lib;

let

  # Variables provided by the generator.
  project-name = @@PROJECT_NAME@@;
  cache-folder = @@CACHE_FOLDER@@;
  cache-hash = @@CACHE_HASH@@;
  yarn-path = @@YARN_PATH@@;
  yarn-closure-entries = @@YARN_CLOSURE_ENTRIES@@;

  # Defines the shell alias to run Yarn.
  yarn-alias = ''
    yarn() {
      CI=1 node "$NIX_YARN_PATH" "$@"
    }
  '';

  # Directory of just the files needed to run Yarn.
  yarn-closure = cleanSourceWith {
    src = ./.;
    filter = let
      srcStr = toString ./.;
      srcRel = removePrefix "${srcStr}/";
    in path: type:
      elem "${type}:${srcRel path}" yarn-closure-entries;
  };

  # Build just the cache for the project.
  offline-cache = stdenv.mkDerivation {
    name = "${project-name}-offline-cache";
    buildInputs = [ nodejs ];
    builder = writeText "builder.sh" ''
      source $stdenv/setup
      cd '${yarn-closure}'

      # Yarn may need a writable home directory for the global cache mirror.
      # TODO: Can't disable the mirror, because it changes cache filenames.
      export HOME="$TEMP"

      # Setup to environment so we can run Yarn.
      export NIX_YARN_PATH="$(readlink -f '${yarn-path}')"
      ${yarn-alias}

      # Invoke a plugin internal command to build the cache.
      yarn nixify build-cache $out
    '';
    outputHashMode = "recursive";
    outputHashAlgo = "sha256";
    outputHash = cache-hash;
  };

in stdenv.mkDerivation {
  name = project-name;
  src = ./.;

  # Tell node-gyp to use the provided Node.js headers for native code builds.
  npm_config_nodedir = nodejs;
  # Tell node-pre-gyp to never fetch binaries / always build from source.
  npm_config_build_from_source = "true";

  # Make sure the build uses the right Node.js version everywhere.
  buildInputs = [ coreutils nodejs ];

  # Define the Yarn alias in the build environment.
  postHook = yarn-alias;

  configurePhase = ''
    runHook preConfigure

    # Copy over the Yarn cache.
    # TODO: Can we do without the copy somehow?
    rm -fr '${cache-folder}'
    mkdir -p "$(dirname '${cache-folder}')"
    cp -r '${offline-cache}' '${cache-folder}'
    chmod -R u+w '${cache-folder}'

    # Store the absolute path to Yarn for the 'yarn' alias.
    export NIX_YARN_PATH="$(readlink -f '${yarn-path}')"

    # Run normal Yarn install to complete dependency installation.
    yarn install --immutable --immutable-cache

    runHook postConfigure
  '';

  buildPhase = ''
    runHook preBuild
    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir -p $out/libexec $out/bin

    # Move the entire project to the output directory.
    mv $PWD "$out/libexec/$sourceRoot"
    cd "$out/libexec/$sourceRoot"

    # Update the path to Yarn.
    export NIX_YARN_PATH="$(readlink -f '${yarn-path}')"

    # Invoke a plugin internal command to setup binaries.
    yarn nixify install-bin $out/bin

    runHook postInstall
  '';

  passthru = {
    inherit yarn-closure offline-cache;
  };
}
